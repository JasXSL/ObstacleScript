// Name the script #Nodes
/*
	Handles:
	- Room detection
	- Hot temps
	- Breaker
	- Lamps
	- Spirit box room detection
	- Hunt room detection
	- Passive arousal generation
	
	Required a defined array
	#define ROOMS - 3-strided array:
		[0] str Label - Shorthand form of the rooms name, in allcaps. Ex a living room might be LROOM
		[1] str User readable - Long name, ex "Living Room"
		[2] int Flags - 0x1 = Room has plumbing (used for gooryo)
	
	// Labels are unique, but user readable names can be used to combine rooms into one. Such as the junction above the stairs
	// Roomindex is based on the strided first index of the ROOMS array.
	
*/
#define USE_STATE_ENTRY
#define USE_TIMER
#define SE_OVERRIDE handleStateEntry();
#include "ObstacleScript/helpers/Ghost/GhostHelper.lsb"
#include "ObstacleScript/index.lsl"


#define getRoomLabel( idx ) idbGetByIndex(idbTable$NODES_ROOM_LABELS, idx)
#define getRoomName( idx ) idbGetByIndex(idbTable$NODES_ROOM_NAMES, idx)

#ifndef debugUncommon
	#define debugUncommon(text)
#endif

list C_ROOMS; // [(int)flags] - The array index is linked to idbTable$NODES_ROOM_LABELS/NAMES



#define HAS_TEMPS (GhostGet$evidence() & GhostConst$evidence$temps)


#define TEMP_OUTDOOR 16
#define TEMP_INDOOR 20
#define TEMP_HOT 30
#define TEMP_MAX 34
#define TEMP_MAX_NO_EVIDENCE 29

#define TEMP_HOT_BREAKPOINT (TEMP_HOT-TEMP_INDOOR)/(TEMP_MAX_NO_EVIDENCE-TEMP_INDOOR)

#define RM_STRIDE NodesConst$rmStride
// Note that the same index can appear multiple times
#define RM_INDEX 0      // (Position in ROOMS list divided by ROOMS stride)
#define RM_POS 1		// Position of the spawn
#define RM_ROT 2		// Rotation of the spawn
#define RM_SCALE 3		// Scale of the spawn

list ROOM_MARKERS = [];	// Contains data from the spawner library

int breakerCharge;	// Set to 5 min when breaker turns on. For each light that's on above maxLights it starts draining until the breaker turns off.
int breaker;		// Breaker on
list roomLights;	// Lights of a room. Each index corresponds to an index in ROOMS. Note that this relies on readable name, which means only the first entry of that readable is toggled.
list roomTemps;		// Temperatures of a room, float between 0 and 1 with no evidence, or 0 and (TEMP_MAX-TEMP_INDOOR)/(TEMP_MAX_NO_EVIDENCE-TEMP_INDOOR). Each index corresponds to an index in ROOMS. Same as above, it uses the first entry of readable name.
float lastSweat;
vector rezPos;
int ghostInLight = -1;	// Sets if the ghost is in a lit room or not
int poppedLight = -1;	// Set to the room index of readable of a room that's got a popped light. Only one light may pop per game for now.
int GR;				// Index of current ghost room

list portals;      // 8bArray roomIndexes, (int)linkNum

// Any lights on above this will start counting down the 
#define maxLights() (count(C_ROOMS)/3)

int GAME_ACTIVE;

integer getRoomIndexByTable( str name, str table ){
	int i;
	for(; i < count(C_ROOMS); ++i ){
		
		if( idbGetByIndex(table, i) == name )
			return i;
	}
	return -1;
}
#define getRoomIndexByLabel(label) getRoomIndexByTable(label, idbTable$NODES_ROOM_LABELS)
#define getRoomIndexByReadable(name) getRoomIndexByTable(name, idbTable$NODES_ROOM_NAMES)

#define portal2names( bitArray8 ) ((list)getRoomLabel(bitArray8&0xFF) + getRoomLabel((bitArray8>>8)&0xFF))

// Returns the absolute positions in the ROOM_MARKERS array where roomIndex is at
// The reason it can return multiple ones is that one room can have multiple markers. Such as the front bedroom which would clip into the bathroom otherwise.
list getRoomMarkersByRoomIndex( integer roomIndex ){
    
    list out;
    list l = llList2ListStrided(ROOM_MARKERS, 0, -1, RM_STRIDE);
    integer i;
    for(; i < count(l); ++i ){
        
        if( l2i(l, i) == roomIndex )
            out += (i*RM_STRIDE);
        
    }
    
    return out;
    
}

// Could probably improve this with a proper pathfinding algorithm
// But I don't expect there to be a huge nr of rooms
// Returns an empty array if it's a dead end
list findShortestPath( string from, string to, list visited ){

    visited += from;
    
    list path;
    integer i;
    for(; i < count(portals); i += 2 ){
        
        list names = portal2names(l2i(portals, i));
        integer pos = llListFindList(names, (list)from);
        // This portal connects to from
        if( ~pos ){

            // Get the room this portal connects us to
            string other = l2s(names, !pos);
            
            // Found the end goal
            if( other == to )
                return (list)from + other;
            
            
            // Ignore if visited to prevent recursion
            if( llListFindList(visited, (list)other) == -1 ){
                
                list test = findShortestPath(other, to, visited);
                if( count(test) && (!count(path) || count(test) < count(path)) )
                    path = test;

            }
            
        }
        
    }
    
    // Dead end
    if( !count(path) )
        return path;
    
    // Returns the shortest path from this node
    return (list)from + path;
    
}

// Gets the UUID of a portal that contains a and b
key getPortalWithNodes( string a, string b ){
    
    integer i;
    for(; i < count(portals); i += 2 ){
        
        list spl = portal2names(l2i(portals, i));
        if(
            ~llListFindList(spl, (list)a) &&
            ~llListFindList(spl, (list)b)
        )return llGetLinkKey(l2i(portals, i+1));
        
    }
    
    return "";
    
}

// Turn a list of nodes into a list of UUIDs of viable portals
list pathToNodes( list nodes ){
    
    list out = [];
    while( count(nodes) > 1 ){
        
        key find = getPortalWithNodes(l2s(nodes, 0), l2s(nodes, 1));
        if( find ){
            
            nodes = llDeleteSubList(nodes, 0, 0);
            out += find;
            
        }
        // Fail
        else
            nodes = [];
        
    }
    return out;
    
}

// Checks if pos is within a bounding box
// i is the absolute position in the ROOM_MARKERS list pointing towards the first element of the stride
integer isPosInRoomMarker( vector pos, integer i ){
    
    rotation bbRot = l2r(ROOM_MARKERS, i+RM_ROT);
    vector bbPos = l2v(ROOM_MARKERS, i+RM_POS)+llGetRootPosition();
    vector bbSize = l2v(ROOM_MARKERS, i+RM_SCALE);       
    bbPos /= bbRot;
    pos /= bbRot;
    return (
        pos.x < bbPos.x+bbSize.x/2 && pos.x > bbPos.x-bbSize.x/2 &&
        pos.y < bbPos.y+bbSize.y/2 && pos.y > bbPos.y-bbSize.y/2 &&
        pos.z < bbPos.z+bbSize.z/2 && pos.z > bbPos.z-bbSize.z/2
    );
    
}


#define GPR_LABEL 0
#define GPR_INDEX 1
#define GPR_READABLE 2
// Returns the room label
string _gpr( vector point, int ret ){
    
    integer i;
    for(; i < count(ROOM_MARKERS); i += RM_STRIDE ){
        
        if( isPosInRoomMarker( point, i) ){
		
			int idx = l2i(ROOM_MARKERS, i);
			if( ret == GPR_INDEX )
				return (str)idx;

			if( ret == GPR_READABLE )
				return getRoomName(idx);

            return getRoomLabel(idx);
        
		}
		
    }
    
    return "";
    
}

#define getPosRoom(point) _gpr(point, GPR_LABEL)
#define getPosReadable(point) _gpr(point, GPR_READABLE)
#define getPosIndex(point) ((int)_gpr(point, GPR_INDEX))

// Returns absolute indexes from the portals list of portals in the room marker
list findPortalsInRoom( integer roomIndex ){
    
    // Get absolute position of room bounding box in 
    list rmPos = getRoomMarkersByRoomIndex(roomIndex);
    list out;
    
    integer p;
    integer i;
    for(; i < count(rmPos); ++i ){
        
        integer markerAbsoluteIndex = l2i(rmPos, i);
        for(; p < count(portals); p += 2 ){
            
            vector pos = prPos(llGetLinkKey(l2i(portals, p+1)));
            if( isPosInRoomMarker(pos, markerAbsoluteIndex) )
                out += p;
            
        }
    }
    return out;
    
}

// Tries to find the closest room label based on proximity from pos
// Used to warp a ghost if they're out of bounds
string getClosestRoom( vector pos ){
    
    string closest = ""; float dist;
    integer i;
    for(; i < count(ROOM_MARKERS);  i += RM_STRIDE ){
        
        vector p = l2v(ROOM_MARKERS, i+RM_POS);
        float d = llVecDist(p, pos);
        if( d < dist || closest == "" ){
            
            dist = d;
            closest = getRoomLabel(l2i(ROOM_MARKERS, i));
            
        }
        
    }
    return closest;
    
}



resetTempData(){

	poppedLight = -1;
	roomLights = [];
	roomTemps = [];
	breaker = false;
	GhostLevelHelper$togglePoweredSounds(breaker);
	integer i;
	for(; i < count(C_ROOMS); ++i ){
		roomTemps += 0.0;
		roomLights += FALSE;
	}
	
}


// Val is a value between 0 and 1
setGhostTemp( int roomIndex, float val ){
	
	float maxTemp = 1.0;
	
	if( HAS_TEMPS && !hasWeakAffix(GhostGet$affixes(), ToolSetConst$affix$noEvidenceUntilSalted) )
		maxTemp = (float)(TEMP_MAX-TEMP_INDOOR)/(TEMP_MAX_NO_EVIDENCE-TEMP_INDOOR);
		
	//qd("Setting " + val + "on" + roomIndex + "max" + maxTemp);
	if( val < 0 )
		val = 0;
	if( val > maxTemp )
		val = maxTemp;
	roomTemps = llListReplaceList(roomTemps, (list)val, roomIndex, roomIndex);

}




// Overrides the main event handler to save memory when booting
handleStateEntry(){
	
	list rindex;
	list tmp = ROOMS;
	idbDropInline(idbTable$NODES_ROOM_LABELS);
	idbDropInline(idbTable$NODES_ROOM_NAMES);
	int i;
	for( ; i < count(tmp); i += 3 ){
		
		C_ROOMS += (list)i + l2i(tmp, i+2);
		idbInsert(idbTable$NODES_ROOM_LABELS, l2s(tmp, i));
		idbInsert(idbTable$NODES_ROOM_NAMES, l2s(tmp, i+1));
		rindex += (list)l2s(tmp, i);
		
	}	
	tmp = [];
	
	rezPos = llGetPos();
	
	// Iterate the spawn table and cache room markers pos, rot, size
	idbForeach(idbTable$SPAWNS, idx, data)
		
		list spawn = llJson2List(data);
		if( l2s(spawn, SpawnerConst$E_GROUP) == "rooms" ){
		
            string label = j(l2s(spawn, SpawnerConst$E_DESC), 1); // Room label is stored as second param of desc
            
            integer pos = llListFindList(rindex, (list)label);
            if( pos == -1 )
                llOwnerSay(label + "room index not found");
            ROOM_MARKERS += (list)
                pos +	// index of stride slice
                (vector)l2s(spawn, SpawnerConst$E_POS) +
                (rotation)l2s(spawn, SpawnerConst$E_ROT) +
                (vector)j(l2s(spawn, SpawnerConst$E_DESC), 0) // Size
            ;
	
		}
		
	end
	
	portals = [];
	list found;
	
	// Sanity check our portals
	links_each( nr, name,
		
		if( name == "PORTAL" ){
			
			list spl = split(prDesc(llGetLinkKey(nr)), ":");
			integer i;
			for(; i < count(spl); ++i ){
				
				string n = l2s(spl, i);
				if( ~llListFindList(found, (list)n ) ){}
				else if( llListFindList(rindex, (list)n) == -1 ){
					llOwnerSay("GhostNodes: Add '" + n + "' to ROOMS");
					return;
				}
				else{
					
					integer pos = llListFindList(rindex, (list)n);
					if( ~pos ){
						rindex = llDeleteSubList(rindex, pos, pos);
						found += n;
					}
					
				}
			}
			
		}
		
	)
	
	if( count(rindex) )
		llOwnerSay("No room markers:" + llList2CSV(rindex));
	
	// Next make a 2-stride list of each portal with the first value containing the two room indexes, and the second being the UUID of the portal
	links_each( nr, name,
		
		if( name == "PORTAL" ){
			list spl = llParseString2List(
			
				l2s(llGetLinkPrimitiveParams(nr, (list)PRIM_DESC), 0), 
				(list)":", 
				[]
			);
			portals +=  (list)(
				getRoomIndexByLabel(l2s(spl, 0)) | 
				(getRoomIndexByLabel(l2s(spl, 1)) << 8)
			) + nr;
			
		}
		
	)
	
	//llOwnerSay("Cached" + count(ROOM_MARKERS)/4 + "Markers");

    
	resetTempData();
	setInterval("TICK", 10);
	setInterval("POS", 1);


}



// BEGIN

#include "ObstacleScript/begin.lsl"

onLevelCustomSpiritBoxTrigger( spiritBox ) 
	
	integer success = TRUE;
	
	str room = getPosReadable(prPos(spiritBox));
	if( room == "" || hasWeakAffix(GhostGet$affixes(), ToolSetConst$affix$noEvidenceUntilSalted) )
		success = FALSE;
	else{
	
		int index = getRoomIndexByReadable(room);
		// Lights must be off
		if( l2i(roomLights, index) && breaker && index != poppedLight ){
			success = false;
			//qd("SB Fail: Lights on");
		}
		// Player must be solo
		else{
		
			int nrPlayers;
			forPlayer(tot, i, t)
			
				str pl = getPosReadable(prPos(t));
				if( pl == room && ~llGetAgentInfo(t) & AGENT_SITTING )
					++nrPlayers;
			
			end
			
			if( nrPlayers < 1 )
				nrPlayers = 1;
			
			success = llFrand(1) < 1.0/nrPlayers;
			/*
			if( !success )
				qd("SB Fail: Too many players" + nrPlayers);
			*/
			
		}
		
	}
	
	float mDist = 4-0.2*GhostGet$difficulty();
	float dist = llVecDist(prPos(spiritBox), prPos(GhostGet$ghost()));
	if( 
		success && (
			~GhostGet$evidence()&GhostConst$evidence$spiritbox || 
			dist > mDist
		)
    ){
        success = FALSE;
		//qd("SB Fail: Too far or not this evidence" + EVIDENCE_TYPES + dist);
    }
    SpiritBox$start( spiritBox, success );

end



// Checks if the level has moved, and also tracks the ghost
handleTimer( "POS" )

	key ghost = GhostGet$ghost();	
	if( llKey2Name(ghost) ){
		
		str room = getPosReadable(prPos(ghost));
		int index = getRoomIndexByReadable(room);
		int lit = l2i(roomLights, index) && breaker && poppedLight != index;
		
		if( lit != ghostInLight ){
			
			ghostInLight = lit;
			GhostAux$setLight( ghostInLight );
		
		}
		
		int playersInGhostRoom;
		forPlayer( t, idx, player )
			
			if( getPosReadable(prPos(player)) == room )
				++playersInGhostRoom;
		
		end
		
		idbSetByIndex(idbTable$NODES, idbTable$NODES$PIGR, (str)playersInGhostRoom);
		
		// Ghost room changed
		if( index != GR ){
		
			GR = index;
			list markers = getRoomMarkersByRoomIndex(index);
			integer i; list set;
			for(; i < count(markers); ++i )
				set += llList2List(ROOM_MARKERS, l2i(markers, i)+RM_POS, l2i(markers, i)+RM_POS+2);
			idbSetByIndex(idbTable$NODES, idbTable$NODES$GRMARKERS, mkarr(set));

		}
	}
	
	if( llVecDist(llGetPos(), rezPos) > .1 ){
	
		//llOwnerSay("Level movement detected, recaching positions in 5 sec");
		llSleep(5);
		llResetScript();
		
	}
	
end


handleEvent( "#Game", 0 )

	str type = argStr(0);
	if( type == "ROUND_START" || type == "END_GAME" ){
			
		GAME_ACTIVE = (type == "ROUND_START");
		resetTempData();
		
	}	
	/*
	else if( type == "DEBUG" ){
        
		qd("Rooms" + ROOMS);
		qd("Breaker"+breaker);
		qd("Temps" + roomTemps);
		qd("Lights" + roomLights + "popped" + poppedLight);
		
    }	
	*/
end

handleEvent( "#GhostBehavior", 0 )

	str type = argStr(0);
	
	// Second step of hunt check. Makes sure at least one player is in the house. Forwards to GhostTools
	if( type == "CTH" ){
		
		forPlayer( t, idx, player )
			
			// Lazy way of checking if player is dead
			if( getPosRoom(prPos(player)) != "" && ~llGetAgentInfo(player) & AGENT_SITTING ){
				raiseEvent(0, "CTH");
				return;
			}

		end

	}
	
end

// Sends a negative ouija board id with the text
onLevelCustomToolsetOuija( sender, type )
	
	if( isHunting() )
		return;
	str room = getPosReadable(prPos(sender));
	if( room == "" )
		return;
    integer success = llFrand(1.0) < .666;
    
    int arousal = 20;
    if( success ){
	
		// Defer to GhostAux which stores the responses
        raiseEvent(NodesEvt$ouija, argKey(0) + type + getPosReadable(prPos(GhostGet$ghost())));    
		if( GhostGet$type() == GhostConst$type$imp )
			return;	/// Don't arouse on imp, return here saves memory
			
    }
    else{
		Lamp$flicker( room, TRUE, 3 );	// Todo: Test
    }
    
	// Tell level to add arousal
	raiseEvent(0, "AROUSE" + llGetOwnerKey(sender) + arousal);

end




handleEvent( "#Tools", 0 )
		
	str type = argStr(0);
	
	// Motion sensors
	if( type == "SEN_NAMES" ){
		
		METHOD_ARGS = llList2List(METHOD_ARGS, 1, 4);
		list positions;
		integer i;
		for(; i < count(METHOD_ARGS); ++i ){
			
			key id = l2k(METHOD_ARGS, i);
			str room = getPosReadable(prPos(id));
			positions += room;
			
		}
		GhostStatus$updateSoundSensors( "*", positions );
	
	}
	
	
	
end


/*
	Handles: 
		Hot temps (30+ temps)
		Sanity decay
*/
// Tick every 10 sec
handleTimer( "TICK" )

	if( !GAME_ACTIVE )
		return;
	
	key ghost = GhostGet$ghost();
	if( llKey2Name(ghost) == "" )
		return;
	
	// Check for ghost
	vector ghostPos = prPos(ghost);
	str room = getPosReadable(ghostPos);	// Only the first readable is used, so we need to get the first one here.
	int ghostRoomIdx = getRoomIndexByReadable(room);
	int affixes = GhostGet$affixes();
	int difficulty = GhostGet$difficulty();
	int type = GhostGet$type();
	
	// Update temperatures
	integer i;
	if( !hasStrongAffix(affixes, ToolSetConst$affix$reqMotionSensor) && !hasStrongAffix(affixes, ToolSetConst$affix$vibrator) ){
	
		for( ; i < count(roomTemps); ++i ){
			
			float val = l2f(roomTemps, i);
			
			float add = 0.2/(difficulty+1);
			if( add < 0.07 )	// caps it as pro rate
				add = 0.07;
			if( i != ghostRoomIdx ) // Fade
				add = -add;
				
			val += add;
			setGhostTemp(i, val);
		}
		
	}
	int npl = numPlayers();
	float multi = 1.0+(npl-1)*.2;	// 20% faster arousal per player above 1
	if( multi > 1.4 )
		multi = 1.4;
	
	// Handle arousal gain
	// Check if player is in a room
	list decay;	// Corresponds to player index
	list swPlayers;	// Keys of players that are in hot rooms
	for( i = 0; i < npl; ++i ){
			
		float amt;	// Start at 0 in case player is outdoors
		
		key player = idbGetByIndex(idbTable$PLAYERS, i);
		str plRoom = getPosReadable(prPos(player));
		// Player is inside a room in the house
		if( plRoom != "" ){
			
			amt = 0.5;	// Check if lights are off
			int plRoomIdx = getRoomIndexByReadable(plRoom);			// Get the index of the readable
			if( !breaker || !l2i(roomLights, plRoomIdx) || poppedLight == plRoomIdx )			// Check if the room is dark
				amt = 1.5;											// 3x drain in the dark 
				
				
			// GHOST BEHAVIOR :: Yuri - drains 50% faster in the same room
			if( plRoom == room && type == GhostConst$type$yuri )	// Player is in the ghost's current room
				amt *= 1.5;
			
			// Next check temps
			float temp = l2f(roomTemps, plRoomIdx);
			if( temp > TEMP_HOT_BREAKPOINT && ~llGetAgentInfo(player) & AGENT_SITTING )
				swPlayers += player;
			
		}
		
		decay += (amt*multi);

	}
	
	if( swPlayers != [] && llGetTime()-lastSweat > 6 && llFrand(1) < .35 ){

		key player = randElem(swPlayers);
		AnimHandler$start(player, "sweaty");
		ToolSet$hotTemps( player );		

	}
	
	// Drain breaker power
	int drainBreaker = (int)llListStatistics(LIST_STAT_SUM, roomLights)-maxLights();
	if( drainBreaker > 0 ){
		
		breakerCharge -= drainBreaker*10;
		if( breakerCharge < 1 && breaker ){
			
			Lamp$toggle( LampConst$breakerID, FALSE ); // This tells the breaker to turn itself off. The breaker will then send a method to us that says it's off.
			
		}
		
	}
	
	raiseEvent(0, "DECAY" + decay);
	
end

onGhostEventsBegin( players, type, subtype )
	
	vector ghostPos = prPos(GhostGet$ghost());
	str room = getPosReadable(ghostPos);
	int tempRoom = getRoomIndexByReadable(room);
	if( ~tempRoom )
		setGhostTemp(tempRoom, l2f(roomTemps, tempRoom)+0.5);	// add 50% hot on a ghost event.
	
end

onLevelCustomLightSwitch( lightSwitch, room, on )
	
	// Note: The breaker object handles toggling of the lights. This script just stores the state.
	if( room == "_BREAKER_" ){
	
		breaker = on;
		breakerCharge = 300; // Reset the breaker timer regardless of if it's on or off
		GhostLevelHelper$togglePoweredSounds(on);
		return;
		
	}
	
	integer index = getRoomIndexByReadable(room);
	if( index == -1 )
		return;
	roomLights = llListReplaceList(roomLights, (list)(on > 0), index, index);
	Lamp$toggle( room, on );
	
end

handleMethod( NodesMethod$popLight )
	
	str readable = argStr(0);
	poppedLight = getRoomIndexByReadable(readable);
	roomLights = llListReplaceList(roomLights, (list)FALSE, poppedLight, poppedLight);
	
end

handleMethod( NodesMethod$getRooms )
	
	integer i;
	for(; i < count(ROOM_MARKERS); i += RM_STRIDE ){
	
		list slice = llList2List(ROOM_MARKERS, i, i+RM_STRIDE-1);
		slice = llListReplaceList(slice, (list)(l2v(ROOM_MARKERS, i+RM_POS)+llGetRootPosition()), RM_POS, RM_POS);
		runMethod(SENDER_KEY, argStr(0), argInt(1), i + slice);
		
	}
	
end
handleMethod( NodesMethod$getPath )

	string ss = argStr(0);
	int cb = argInt(1);
	vector startPos = argVec(2);
	string room = argStr(3);
	if( (vector)room != ZERO_VECTOR )
		room = getPosRoom((vector)room);
		
	// Get the index of the room the ghost is in
    string currentRoom = getPosRoom(startPos);
    // Already there!
    if( currentRoom == room )
        return;
    
    // Out of bounds, we'll have to improvise
    if( currentRoom == "" )
        currentRoom = getClosestRoom(startPos);

    integer ri = getRoomIndexByLabel(currentRoom);
    // Start by trying to find a node in the current room
    if( ~ri ){

        list path = findShortestPath(currentRoom, room, []);
        debugUncommon("Trying to path from "+currentRoom+"to"+room);
        list nodes = pathToNodes(path);
        debugUncommon(path);
        debugUncommon(nodes);
        runMethod(SENDER_KEY, ss, cb, nodes);
        
    }
    

end

handleOwnerMethod( NodesMethod$getPlumbedRoom )
	
	str cb = argStr(0);
	str senderScript = argStr(1);
	int cbMethod = argInt(2);
	int i;
	list viableIdx;
	for(; i < count(C_ROOMS); ++i ){
		
		// May go to the current room, but that's fine to keep the ghost there a bit longer
		if( l2i(C_ROOMS, i) & ROOM_PLUMBED )
			viableIdx += i;
		
	}
	if( viableIdx == [] )
		return;

	int ri = l2i(viableIdx, floor(llFrand(count(viableIdx))));
	list markers = getRoomMarkersByRoomIndex( ri );
	if( markers == [] )
		return;
	
	int out = l2i(markers, floor(llFrand(count(markers))));
	vector pos = l2v(ROOM_MARKERS, out+RM_POS);
	
	runMethod(SENDER_KEY, senderScript, cbMethod, cb + (pos+llGetRootPosition()));
	
end

// Callbakcs with cbString, roomLabel, roomName (readable)
handleMethod( NodesMethod$getRoomName )

	str cbString = argStr(0);
	vector ppos = argVec(1);
	str ss = argStr(2);
	int cb = argInt(3);
	
	integer idx = getPosIndex(ppos);
	list out;
	if( ~idx )
		out = (list)getRoomLabel(idx) + getRoomName(idx);
		
	list re = (list)SENDER_KEY;
	if( SENDER_KEY == "" )
		re = (list)LINK_THIS;
	runMethod(re, ss, cb, cbString + out );
	

end

// Sends a temperature reading based on position
handleMethod( NodesMethod$getTemp )

	vector vpos = argVec(0);
	key targ = argKey(1);
	str targScript = argStr(2);
	int targMethod = argInt(3);
	
	float temp = 15;
	string room = getPosRoom(vpos);
	if( room ){
	
		integer ri = getRoomIndexByLabel(room);
		ri = getRoomIndexByReadable(getRoomName(ri));	// Only the first readable is used, so we need to get the first one here
		temp = l2f(roomTemps, ri);
		if( temp < 1 )
			temp *= temp;
		temp *= (TEMP_MAX_NO_EVIDENCE-TEMP_INDOOR);
		temp += TEMP_INDOOR;
		
	}
		
	runMethod(targ, targScript, targMethod, temp);
	

end

onLevelCustomGhostSpawned( ghost )
	ghostInLight = -1;
end

#include "ObstacleScript/end.lsl"


