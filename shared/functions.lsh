#ifndef __Functions
#define __Functions

#include "../headers/Rlv.lsh"
#include "../headers/SupportCube.lsh"

// Actually triggers in a cube
_tsr( key sound, float vol, float radius ){
	
	vector pos = llGetPos();
	vector v = <radius, radius, radius>;
	llTriggerSoundLimited(sound, vol, pos+v, pos-v);

}
#define triggerSoundRadius( sound, vol, radius ) _tsr(sound, vol, radius)

// get number of arguments with __NARG__
#define __NARG__(...)  __NARG_I_(__VA_ARGS__,__RSEQ_N())
#define __NARG_I_(...) __ARG_N(__VA_ARGS__)
#define __ARG_N( \
      _1, _2, _3, _4, _5, _6, _7, _8, _9,_10, \
     _11,_12,_13,_14,_15,_16,_17,_18,_19,_20, \
     _21,_22,_23,_24,_25,_26,_27,_28,_29,_30, \
     _31,_32,_33,_34,_35,_36,_37,_38,_39,_40, \
     _41,_42,_43,_44,_45,_46,_47,_48,_49,_50, \
     _51,_52,_53,_54,_55,_56,_57,_58,_59,_60, \
     _61,_62,_63,N,...) N
#define __RSEQ_N() \
     63,62,61,60,                   \
     59,58,57,56,55,54,53,52,51,50, \
     49,48,47,46,45,44,43,42,41,40, \
     39,38,37,36,35,34,33,32,31,30, \
     29,28,27,26,25,24,23,22,21,20, \
     19,18,17,16,15,14,13,12,11,10, \
     9,8,7,6,5,4,3,2,1,0

// general definition for any function name
#define _VFUNC_(name, n) name##n
#define _VFUNC(name, n) _VFUNC_(name, n)
#define OVERLOAD(func, ...) _VFUNC(func, __NARG__(__VA_ARGS__)) (__VA_ARGS__)

// LSD functions
// Note: Do not start your idx with a unicode value below 32
// Warning: Indexed table names should be exactly 1 character to prevent collissions with other tables.
// Warning: Non indexed table names should have at least 3 characters.
// To save memory, there are no guardrails preventing you from fucking that up.
#define idbSet(table, field, val) llLinksetDataWrite(table+(str)(field), (str)(val))
#define idbGet(table, field) llLinksetDataRead(table+(str)(field))
#define idbUnset(table, field) llLinksetDataDelete(table+(str)(field))

#define idbGetByIndex(table, index) llLinksetDataRead(table+llChar(index+32))
#define idbSetByIndex(table, index, val) llLinksetDataWrite(table+llChar(index+32), (str)(val))
#define idbDeleteByIndex(table, index) llLinksetDataDelete(table+llChar(index+32))

#define idbSetIndex(table, val) llLinksetDataWrite(table, (str)(val))
#define idbResetIndex(table) llLinksetDataWrite(table, "0")
#define idbGetIndex(table) (int)llLinksetDataRead(table)
// Inserts and maintains an index
integer _dbi( string table, string data ){

	integer nr = idbGetIndex(table);
	idbSet(table, llChar(nr+32), data);
	idbSetIndex(table, nr+1);
	return nr;

}
#define idbInsert(table, data) _dbi((table), (string)(data))

// Get all values from a table
#define idbValues(table, full) _ia(table, full)
list _ia( string t, integer f ){
	integer max = idbGetIndex(t);
	integer i; list out;
	for(; i < max; ++i ){
		str data = idbGetByIndex(t, i);
		if( data != "" || f )
			out += data;
	}
	return out;
}

// Drop an indexed table. Relatively memory intensive.
#define idbDropInline(table) \
	/* Delete the index table */ \
	llLinksetDataDelete(table); \
	list found; integer i; \
	/* Delete any data that matches table+1char */  \
	while( (found = llLinksetDataFindKeys("^"+table+".{1}$", 0, 100)) != [] ){ \
		for( i = 0; i < count(found); ++i ) \
			llLinksetDataDelete(l2s(found, i)); \
	}


_dbd( string table ){
	idbDropInline(table)
}
#define idbDrop(table) _dbd(table)

#define idbForeach(table, idx, row) \
	integer idx; \
	for(; idx < idbGetIndex(table); ++idx ){ \
		string row = idbGetByIndex(table, idx);




// Lets you round floats, vectors, and rotations
#define allRound(input, places) _allRound((list)(input), places)
string _allRound( list input, integer places ){
    
    list vals = (list)llList2Float(input, 0); 
    integer type = llGetListEntryType(input, 0);
    if( type == TYPE_VECTOR ){
        vector v = llList2Vector(input, 0);
        vals = (list)v.x + v.y + v.z;
    }
    else if( type == TYPE_ROTATION  ){
        rotation v = llList2Rot(input, 0);
        vals = (list)v.x + v.y + v.z + v.s;
    }
    
    float exponent = llPow(10,places);
    integer i;
    for( ; i<llGetListLength(vals); ++i ){
        
        
        string v = (string)(
            (float)llRound(llList2Float(vals, i)*exponent)
            /exponent
        );
        while( llGetSubString(v, -1, -1) == "0" )
            v = llDeleteSubString(v, -1, -1);
        
        while( llGetSubString(v, 0, 0) == "0" )
            v = llDeleteSubString(v, 0, 0);
            
        if( llGetSubString(v, -1, -1) == "." )
            v = llDeleteSubString(v, -1, -1);
        
        if( v == "" )
            v = "0";
        
        vals = llListReplaceList(vals, (list)v, i, i);
        
    }
    
    if( llGetListLength(vals) > 1 )
        return "<"+llDumpList2String(vals, ",")+">";
    return llList2String(vals, 0);
}


// Tries to generate a KFM from JSON
list _kfmConv( list input ){

	integer i;
	for(; i < count(input); ++i ){
		
		if( llGetSubString(l2s(input, i), 0, 0) == "<" ){
			
			list spl = split(l2s(input, i), ",");
			if( count(spl) == 4 ){
			
				rotation Q = (rotation)l2s(input, i);
				float MagQ = llSqrt(Q.x*Q.x + Q.y*Q.y +Q.z*Q.z + Q.s*Q.s);
				input = llListReplaceList(input, (list)<Q.x/MagQ, Q.y/MagQ, Q.z/MagQ, Q.s/MagQ>, i, i);
				
			}
			else
				input = llListReplaceList(input, (list)[(vector)l2s(input, i)], i, i);
			
		}
		else
			input = llListReplaceList(input, (list)l2f(input, i), i, i);
		
	}
	
	return input;
	
}


// Telports player to a position. A ray is cast from position to the ground and tries to place the player there
_w2s( key player, vector checkpoint, rotation r, integer unsit ){
    
    list ray = llCastRay(checkpoint, checkpoint-<0,0,25>, RC_DEFAULT);
    vector rp = l2v(ray, 1);
    vector ascale = llGetAgentSize(player);
    checkpoint.z = rp.z+ascale.z/2;

    if( unsit ){
        Rlv$teleportPlayer(
            player, 
            checkpoint,
            r
        );
        return;
    }
    Rlv$teleportPlayerNoUnsit(
        player, 
        checkpoint,
        r
    );
    
}
#define warpPlayerToSurface( player, pos, rot, allowUnsit ) \
	_w2s( player, pos, rot, allowUnsit )

// Returns TRUE if player is looking at targ (positive X)
int _pla( key player, key targ, float angle ){
	
	vector temp = (prPos(targ)-prPos(player))/prRot(player); 
	return llFabs(llAtan2(temp.y,temp.x)) < angle;

}
#define agentLookingAt( player, targ ) \
	_pla(player, targ, PI_BY_TWO)

#define agentLookingAtRadius( player, targ, radius ) \
	_pla(player, targ, radius)
	

pbrAlpha( integer link, float alpha, integer side, integer alphaMode ){
		
	int i = 1;
	for( ; i <= llGetNumberOfPrims(); ++i ){
		
		
		if( 
			(link == LINK_SET) ||
			(link == LINK_ALL_OTHERS && i != llGetLinkNumber()) ||
			(link == LINK_THIS && i == llGetLinkNumber()) ||
			(i == link)
		){
			
			list out = [];
			integer s;
			for(; s < llGetLinkNumberOfSides(i); ++s ){
				
				if( s == side || side == ALL_SIDES )
					out += (list)PRIM_GLTF_BASE_COLOR + s + llListReplaceList(llGetLinkPrimitiveParams(link, (list)PRIM_GLTF_BASE_COLOR + s), (list)alpha + alphaMode, 5,6);
				
			}
			
			llSetLinkPrimitiveParamsFast(i, out);
			
		}

	}
	
}
	
	
	
// returns a viable wall for ghost events [(vector)pos, (vector)normal] if successful
list findWall( key avatar, float offs ){
    
    vector pos = prPos(avatar);
    int dir;
    for(; dir < 4; ++dir ){
        
        list ray = llCastRay(
            pos, 
            pos+<3,0,0>*llEuler2Rot(<0,0,PI_BY_TWO*dir>), 
            RC_DEFAULT + RC_DATA_FLAGS + RC_GET_NORMAL
        );
        vector norm = l2v(ray, 2);
        if( llKey2Name(l2k(ray, 0)) == "WALL" && llFabs(norm.z) < 0.05 ){
            
            norm.z = 0;
            norm = llVecNorm(norm);
            vector offs = l2v(ray, 1)+norm*offs;
            // Get floor
            list r2 = llCastRay(offs, offs-<0,0,3>, RC_DEFAULT + RC_DATA_FLAGS + RC_GET_NORMAL );
            vector fn = l2v(r2, 2);
            if( l2i(r2, -1) == 1 && fn.z > 0.95 ){
                
				return (list)l2v(r2, 1) + norm;
				
            }
            
        }
        
    }
    return [];
    
}

// Returns a position of ceiling for ghost events
vector ceilingPos(){
    list ray = llCastRay(llGetPos()+<0,0,.5>, llGetPos()+<0,0,5>, RC_DEFAULT + RC_DATA_FLAGS + RC_GET_NORMAL);
    if( l2i(ray, -1) != 1 )
        return ZERO_VECTOR;
    vector n = l2v(ray, 2);
    if( n.z > -0.95 )
        return ZERO_VECTOR;
    return l2v(ray, 1);
}

#endif
